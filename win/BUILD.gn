import("//build/config/win/toolchain.gni")

config("debug") {
}

config("release") {
  defines = [ "NDEBUG" ]

  cflags = [
    "/GL",  # LTCG.
    "/O2",
    "/Ob2",  # Both explicit and auto inlining.
    "/Oy-",  # Disable omitting frame pointers, must be after /O2.
    "/Zc:inline",  # Remove unreferenced COMDAT (faster links).
    "/d2Zi+",  # Improve debugging of optimized code.
  ]
  ldflags = [
    "/OPT:ICF",
    "/OPT:REF",
    "/LTCG",
  ]
  arflags = [
    "/LTCG",
  ]
}

config("default") {
  cflags = [
    "/DNOMINMAX",
    "/DUNICODE",
    "/DWIN32_LEAN_AND_MEAN",
    "/D_CRT_SECURE_NO_WARNINGS",
    "/D_HAS_EXCEPTIONS=0",
    "/D_UNICODE",
    "/W4",
    "/WX",
    "/wd4100",  # Unreferenced formal parameter.
    "/wd4127",  # Conditional expression is constant.
    "/wd4351",  # New behavior: elements of array will be default initialized.
    "/wd4530",  # Exceptions disabled.
    "/wd4702",  # Unreachable code.
    "/wd4996",  # 'X' was declared deprecated.
  ]

  if (is_debug) {
    configs = [ ":debug" ]
  } else {
    configs = [ ":release" ]
  }
}

# 32-bit toolchains. Only define these when the target architecture is 32-bit
# since we don't do any 32-bit cross compiles when targeting 64-bit (the
# build does generate some 64-bit stuff from 32-bit target builds).
if (target_cpu == "x86") {
  x86_toolchain_data = exec_script(setup_toolchain_path,
                                   [
                                     visual_studio_path,
                                     windows_sdk_path,
                                     visual_studio_runtime_dirs,
                                     "x86",
                                     "${sys_include_prefix}",
                                   ],
                                   "scope")

  msvc_toolchain("x86") {
    environment = "environment.x86"
    cl = "\"${x86_toolchain_data.vc_bin_dir}/cl.exe\""
    toolchain_args = {
      current_cpu = "x86"
    }
  }
}

# 64-bit toolchains.
x64_toolchain_data = exec_script(setup_toolchain_path,
                                 [
                                   visual_studio_path,
                                   windows_sdk_path,
                                   visual_studio_runtime_dirs,
                                   "x64",
                                   "${sys_include_prefix}",
                                 ],
                                 "scope")

msvc_toolchain("x64") {
  environment = "environment.x64"
  cl = "\"${x64_toolchain_data.vc_bin_dir}/cl.exe\""

  toolchain_args = {
    current_cpu = "x64"
  }
}

# WinRT toolchains. Only define these when targeting them.
#
# NOTE: This is currently broken because it references vc_bin_dir. brettw@
# changed this around a bit, and I don't know what this should be set to
# in terms of what setup_toolchain returns for a certain CPU architecture.
if (target_os == "winrt_81" || target_os == "winrt_81_phone" ||
    target_os == "winrt_10") {
  msvc_toolchain("winrt_x86") {
    environment = "environment.winrt_x86"
    cl = "\"${vc_bin_dir}/cl.exe\""

    toolchain_args = {
      current_cpu = "x86"
    }
  }

  msvc_toolchain("winrt_x64") {
    environment = "environment.winrt_x64"
    cl = "\"${vc_bin_dir}/cl.exe\""

    toolchain_args = {
      current_cpu = "x64"
    }
  }
}
